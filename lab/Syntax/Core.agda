{-
This module exposes the Internal syntax. A term is either checkable or
inferable. As the name indicates, a term of type CheckableTerm is a
term we must check. Similarly, a term of type InferableTerm, it is a
term we can infer.
-}

module MiniJuvix.Syntax.Core 
  where

--------------------------------------------------------------------------------

open import Haskell.Prelude
open import Agda.Builtin.Equality

--------------------------------------------------------------------------------
-- Haskell stuff 
--------------------------------------------------------------------------------

{-# FOREIGN AGDA2HS
{-# OPTIONS_GHC -fno-warn-missing-export-lists #-}
{-# OPTIONS_GHC -fno-warn-unused-matches #-}
{-# OPTIONS_GHC -fno-warn-unused-imports #-}
#-}

{-# FOREIGN AGDA2HS
--------------------------------------------------------------------------------

import MiniJuvix.Prelude

--------------------------------------------------------------------------------
-- Quantity (a.k.a. Usage)
--------------------------------------------------------------------------------
#-}

data Quantity : Set where
  Zero One Many : Quantity
{-# COMPILE AGDA2HS Quantity #-}

instance
  QuantityEq : Eq Quantity
  QuantityEq ._==_ Zero Zero = true
  QuantityEq ._==_ One  One  = true
  QuantityEq ._==_ Many Many = true
  QuantityEq ._==_ _    _    = false
{-# COMPILE AGDA2HS QuantityEq #-}
 
compareQuantity : Quantity -> Quantity -> Ordering
compareQuantity Zero  Zero = EQ
compareQuantity Zero  _    = LT
compareQuantity _    Zero  = GT
compareQuantity One  One   = EQ
compareQuantity One  _     = LT
compareQuantity _    One   = GT
compareQuantity Many Many  = EQ
{-# COMPILE AGDA2HS compareQuantity #-}

instance
  QuantityOrd : Ord Quantity
  QuantityOrd .compare  = compareQuantity 
  QuantityOrd ._<_  x y = compareQuantity x y == LT
  QuantityOrd ._>_  x y = compareQuantity x y == GT
  QuantityOrd ._<=_ x y = compareQuantity x y /= GT
  QuantityOrd ._>=_ x y = compareQuantity x y /= LT
  QuantityOrd .max  x y = if compareQuantity x y == LT then y else x
  QuantityOrd .min  x y = if compareQuantity x y == GT then y else x
  -- Using ordFromCompare didnn' work, I might need to open an issue
  -- for this in agda2hs, Idk.

-- The type of usages forms an ordered semiring.

instance
  QuantitySemigroup : Semigroup Quantity
  QuantitySemigroup ._<>_ Zero _ = Zero
  QuantitySemigroup ._<>_ One m = m
  QuantitySemigroup ._<>_ Many Zero = Zero
  QuantitySemigroup ._<>_ Many One = Many
  QuantitySemigroup ._<>_ Many Many = Many

  QuantityMon : Monoid Quantity
  QuantityMon .mempty = Zero

  QuantitySemiring : Semiring Quantity
  QuantitySemiring .one = One
  QuantitySemiring .times Zero _ = Zero
  QuantitySemiring .times One m = m
  QuantitySemiring .times Many Zero = Zero
  QuantitySemiring .times Many One = Many
  QuantitySemiring .times Many Many = Many

{-# COMPILE AGDA2HS QuantityOrd #-}
{-# COMPILE AGDA2HS QuantitySemigroup #-}
{-# COMPILE AGDA2HS QuantityMon #-}
{-# COMPILE AGDA2HS QuantitySemiring #-}

--------------------------------------------------------------------------------
-- Being relevant for a term is to have non zero quantity.

data Relevance : Set where
  Relevant   : Relevance  -- terms to compute.
  Irrelevant : Relevance  -- terms to contemplate (for type formation).
{-# COMPILE AGDA2HS Relevance #-}
{-# FOREIGN AGDA2HS
deriving stock instance Eq Relevance
deriving stock instance Ord Relevance
#-}

relevancy : Quantity â†’ Relevance
relevancy Zero = Irrelevant
relevancy _    = Relevant
{-# COMPILE AGDA2HS relevancy #-}

--------------------------------------------------------------------------------
-- Variables. Relevant on the following design is the separation for a
-- variable between Bound and Free as a data constructr, due to
-- McBride and McKinna in "Functional Pearl: I am not a Numberâ€”I am a
-- Free Variable". 
--------------------------------------------------------------------------------

-- DeBruijn index.
Index : Set
Index = Nat
{-# COMPILE AGDA2HS Index #-}

-- A variable can be "bound", "binding bound", or simply free. For
-- example, consider  the term "x(Î»y.y)". The variable x is free, the
-- first y is a binding bound variable, and the latter y is bound.

BindingName : Set
BindingName = String
{-# COMPILE AGDA2HS BindingName #-}

-- A named variable can be in a local or global enviroment. In the
-- lambda term above, for example, the second occurrence of y is
-- globally bound. However, inside the the body of the lambda, the
-- variable is local free.

data Name : Set where
  -- the variable has zero binding 
  Global : String â†’ Name
  -- the variable has a binding in its scope.
  Local : BindingName â†’ Index â†’ Name
{-# COMPILE AGDA2HS Name #-}

instance
  nameEq : Eq Name
  nameEq ._==_ (Global x) (Global y) = x == y
  nameEq ._==_ (Local x1 y1) (Local x2 y2) = x1 == x2 && y1 == y2
  nameEq ._==_ _ _ = false
{-# COMPILE AGDA2HS nameEq #-}

-- A variable is then a number indicating its DeBruijn index.
-- Otherwise, it is free, with an identifier as a name, or
-- inside 
data Variable : Set where
  Bound : Index â†’ Variable
  Free  : Name â†’ Variable
{-# COMPILE AGDA2HS Variable #-}

instance
  variableEq : Eq Variable 
  variableEq ._==_ (Bound x) (Bound y) = x == y
  variableEq ._==_ (Free x) (Free y) = x == y
  variableEq ._==_ _ _ = false
{-# COMPILE AGDA2HS variableEq #-}

-- TODO: May I want to have Instances of Ord, Functor, Applicative, Monad?

{-# FOREIGN AGDA2HS
--------------------------------------------------------------------------------
#-}

{- 
Core syntax follows the pattern design for bidirectional typing
algorithmgs in [Dunfield and Krishnaswami, 2019]. Pfenning's principle
is one of such criterion and stated as follows.

1. If the rule is an introduction rule, make the principal judgement
   "checking", and 
2. if the rule is an elimination rule, make the principal judgement
   "synthesising".
   
Jargon:
- Principal connective of a rule:
  - for an introduction rule is the connective that is being
    introduced.
  - for a elimination rule is the connective that is eliminated.
- Principal Judgement of a rule is the judgment that contains the
  principal connective.
-}

{-# FOREIGN AGDA2HS
--------------------------------------------------------------------------------
-- Type-checkable terms.
--------------------------------------------------------------------------------
#-}

data CheckableTerm : Set
data InferableTerm : Set

data CheckableTerm where
  {- Universe types. 
  See the typing rule Univâ‡.
  -}
  UniverseType : CheckableTerm
  {- Dependent function types. 
  See the typing rules â†’Fâ‡ and â†’Iâ‡.
    1. (Î [ x :Ï S ] P x) : U
    2. (Î» x. t) : Î [ x :Ï S ] P x
  -}
  PiType : Quantity â†’ BindingName â†’ CheckableTerm â†’ CheckableTerm â†’ CheckableTerm
  Lam : BindingName â†’ CheckableTerm â†’ CheckableTerm
  {- Dependent tensor product types. 
  See the typing rules âŠ—-F-â‡,  âŠ—-Iâ‚€â‡, and âŠ—-Iâ‚â‡.
    1. * S âŠ— T : U
    2. (M , N) : S âŠ— T
  -}
  TensorType : Quantity â†’ BindingName â†’ CheckableTerm â†’ CheckableTerm â†’ CheckableTerm
  TensorIntro : CheckableTerm â†’ CheckableTerm â†’ CheckableTerm
  {- Unit types. 
  See the typing rule 1-F-â‡ and 1-I-â‡.
    1. ğŸ™ : U
    2. â‹† : ğŸ™
  -}
  UnitType : CheckableTerm
  Unit : CheckableTerm
  {- Disjoint sum types.
  See the typing rules
    1. S + T : U
    2. inl x : S + T
    3. inr x : S + T
  -}
  SumType : CheckableTerm â†’ CheckableTerm â†’ CheckableTerm
  Inl : CheckableTerm â†’ CheckableTerm
  Inr : CheckableTerm â†’ CheckableTerm
  -- Inferrable terms are clearly checkable, see typing rule Infâ‡.
  Inferred : InferableTerm â†’ CheckableTerm

{-# COMPILE AGDA2HS CheckableTerm #-}

{-# FOREIGN AGDA2HS
--------------------------------------------------------------------------------
-- Type-inferable terms (a.k.a terms that synthesise)
--------------------------------------------------------------------------------
#-}

data InferableTerm where
  -- | Variables, typing rule Varâ‡’. 
  Var : Variable â†’ InferableTerm
  -- | Annotations, typing rule Annâ‡’.
  {- Maybe, I want to have the rules here like this:
  
    OÎ“ âŠ¢ S â‡0 ğ•Œ     Î“ âŠ¢ M â‡0 ğ•Œ
    Â­â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€ Annâ‡’
           Î“ âŠ¢ (M : S) â‡’ S
  -}
  Ann : CheckableTerm â†’ CheckableTerm â†’ InferableTerm
  -- |  Application (eliminator).
  App : InferableTerm â†’ CheckableTerm â†’ InferableTerm
  -- | Dependent Tensor product eliminator. See section 2.1.3 in Atkey 2018.
  -- let z@(u, v) = M in N :^q (a âŠ— b))
  TensorTypeElim
    : Quantity       -- q is the multiplicity of the eliminated pair.
    â†’ BindingName          -- z is the name of the variable binding the pair in the
                     -- type annotation of the result of elimination.
    â†’ BindingName          -- u is the name of the variable binding the first element.
    â†’ BindingName          -- v is the name of the variable binding the second element.
    â†’ InferableTerm  -- (u,v) is the eliminated pair.
    â†’ CheckableTerm  -- Result of the elimination.
    â†’ CheckableTerm  -- Type annotation of the result of elimination.
    â†’ InferableTerm
  -- | Sum type eliminator (a.k.a. case)
  -- let (z : S + T) in (case z of {(inl u) â†¦ r1; (inr v) â†¦ r2}  :^q  T) 
  SumTypeElim        -- Case
    :  Quantity      -- Multiplicity of the sum contents.
    â†’  BindingName         -- Name of the variable binding the sum in the type
                     -- annotation of the result of elimination.
    â†’ InferableTerm  -- The eliminated sum.
    â†’ BindingName          -- u is the name of the variable binding the left element.
    â†’ CheckableTerm  -- r1 is the result of the elimination in case the sum contains
                     -- the left element.
    â†’ BindingName          -- v is the name of the variable binding the right element.
    â†’ CheckableTerm  -- r2 is the result of the elimination in case the sum contains
                     -- the right element.
    â†’ CheckableTerm  -- Type annotation of the result of the elimination.
    â†’ InferableTerm
{-# COMPILE AGDA2HS InferableTerm #-}

{-# FOREIGN AGDA2HS
--------------------------------------------------------------------------------
-- Term Equality
--------------------------------------------------------------------------------
#-}

checkEq : CheckableTerm â†’ CheckableTerm â†’ Bool
inferEq : InferableTerm â†’ InferableTerm â†’ Bool

-- We below explicitly use `checkEq` and `inferEq` to have a more
-- reliable Haskell output using Agda2HS. The traditional way gives an
-- extraneous instance definitions.

checkEq UniverseType UniverseType = true
checkEq (PiType qâ‚ _ aâ‚ bâ‚) (PiType qâ‚‚ _ aâ‚‚ bâ‚‚) 
  = qâ‚ == qâ‚‚ && checkEq aâ‚ aâ‚‚ && checkEq bâ‚ bâ‚‚
checkEq (TensorType qâ‚ _ aâ‚ bâ‚) (TensorType qâ‚‚ _ aâ‚‚ bâ‚‚)
   = qâ‚ == qâ‚‚ && checkEq aâ‚ aâ‚‚ &&  checkEq bâ‚ bâ‚‚
checkEq (TensorIntro xâ‚ yâ‚) (TensorIntro xâ‚‚ yâ‚‚) = checkEq xâ‚ xâ‚‚ && checkEq yâ‚ yâ‚‚
checkEq UnitType UnitType = true
checkEq Unit Unit = true
checkEq (SumType xâ‚ yâ‚) (SumType xâ‚‚ yâ‚‚) = checkEq xâ‚ xâ‚‚ && checkEq yâ‚ yâ‚‚
checkEq (Inl x) (Inl y) = checkEq  x y
checkEq (Inr x) (Inr y) = checkEq  x y
checkEq (Inferred x) (Inferred y) = inferEq x y
checkEq _ _ = false
{-# COMPILE AGDA2HS checkEq #-}

inferEq (Var x) (Var y) = x == y
inferEq (Ann xâ‚ yâ‚) (Ann xâ‚‚ yâ‚‚) = checkEq xâ‚ xâ‚‚ &&  checkEq yâ‚ yâ‚‚
inferEq (App xâ‚ yâ‚) (App xâ‚‚ yâ‚‚) =  inferEq xâ‚ xâ‚‚ &&  checkEq yâ‚ yâ‚‚
inferEq (TensorTypeElim qâ‚ _ _ _ aâ‚ bâ‚ câ‚) (TensorTypeElim qâ‚‚ _ _ _ aâ‚‚ bâ‚‚ câ‚‚) 
  = qâ‚ == qâ‚‚ &&  inferEq aâ‚ aâ‚‚ &&  checkEq bâ‚ bâ‚‚ &&  checkEq câ‚ câ‚‚
inferEq (SumTypeElim qâ‚ _ xâ‚ _ aâ‚ _ bâ‚ câ‚) 
      (SumTypeElim qâ‚‚ _ xâ‚‚ _ aâ‚‚ _ bâ‚‚ câ‚‚)
  = qâ‚ == qâ‚‚ &&  inferEq xâ‚ xâ‚‚ && checkEq aâ‚ aâ‚‚ &&  checkEq bâ‚ bâ‚‚ &&  checkEq câ‚ câ‚‚
inferEq _ _ = false
{-# COMPILE AGDA2HS inferEq #-}

instance
  CheckableTermEq : Eq CheckableTerm
  CheckableTermEq ._==_ = checkEq
{-# COMPILE AGDA2HS CheckableTermEq #-}

instance
  InferableTermEq : Eq InferableTerm
  InferableTermEq ._==_ = inferEq
{-# COMPILE AGDA2HS InferableTermEq #-}

data Term : Set where
  Checkable : CheckableTerm â†’ Term  -- terms with a type checkable.
  Inferable : InferableTerm â†’ Term  -- terms that which types can be inferred.
{-# COMPILE AGDA2HS Term #-}

termEq : Term â†’ Term â†’ Bool
termEq (Checkable (Inferred x))  (Inferable y) = x == y
termEq (Checkable x) (Checkable y) = x == y
termEq (Inferable x) (Checkable (Inferred y)) = x == y
termEq (Inferable x) (Inferable y) = x == y
termEq _ _ = false
{-# COMPILE AGDA2HS termEq #-}

instance 
  TermEq : Eq Term 
  TermEq ._==_ = termEq

{-# COMPILE AGDA2HS TermEq #-}   

--------------------------------------------------------------------------------
-- Other Instances
--------------------------------------------------------------------------------