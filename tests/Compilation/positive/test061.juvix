--- Traits
module test061;

import Stdlib.Prelude open hiding {Show; mkShow; show};

trait
type Show A :=
  mkShow {
    show : A → String
  };

instance
showBoolI : Show Bool := mkShow (show := λ{x := if x "true" "false"});

instance
showNatI : Show Nat := mkShow (show := natToString);

showList {A} : {{Show A}} → List A → String
  | nil := "nil"
  | (h :: t) := Show.show h ++str " :: " ++str showList t;

g : {A : Type} → {{Show A}} → Nat := 5;

instance
showListI {A} {{Show A}} : Show (List A) := mkShow (show := showList);

f {A} {{Show A}} : A → String
  | x := Show.show x;

f' {A} : {{Show A}} → A → String
  | {{mkShow s}} x := s x;

f'' {A} : {{Show A}} → A → String
  | {{M}} x := Show.show x;

main : IO :=
  printStringLn (Show.show true) >>
  printStringLn (f false) >>
  printStringLn (Show.show 3) >>
  printStringLn (Show.show g) >>
  printStringLn (Show.show [true; false]) >>
  printStringLn (Show.show [1; 2; 3]) >>
  printStringLn (f' [1; 2]);
