module PackageDescription.V3;

import PackageDescription.V2 open hiding {Package; module Package; mkPackage; defaultPackage} public;
import Juvix.Builtin.V1 open public;

--- A ;RegistryLocation; defines the location of a dependency registry.
type RegistryLocation :=
  | --- A git repository containing a registry
    gitLocation {-- The URL to the git repository
    url : String}
  | --- A git repository containing a registry with ref
    gitLocationRef {
      -- The URL to the git repository
      url : String;
      -- The git ref to checkout
      ref : String
    }
  | --- The default Registry location defined by the Juvix compiler
    defaultLocation;

githubLocation (org repo : String) : RegistryLocation :=
  gitLocation ("https://github.com/" ++str org ++str "/" ++str repo);

--- A ;PackageSet; defines a collection of packages that are known to work together.
type PackageSet :=
  mkPackageSet {
    --- The location of the registry containing the package set
    registryLocation : RegistryLocation;
    --- The version of the package set in the registry
    packageSetVersion : SemVer
  };

type OverridingDependency :=
  mkOverridingDependency {
    --- The dependency to override
    dependency : String;
    --- The overriding dependency version
    overridingVersion : String
  };

dependencyOverride (dependency version : String) : OverridingDependency :=
  mkOverridingDependency@{
    dependency;
    overridingVersion := version
  };

type PackageDependencies :=
  | --- Dependencies from a package set
    packageSetDependencies {
      --- A dependency package set
      packageSet : PackageSet;
      --- Packages to depend on from the package set
      dependencies : List String;
      --- Packages to depend on that are not in the package set
      extraDependencies : List Dependency;
      --- Packages to depend on from the package set with version overrides
      overridingDependencies : List OverridingDependency
    }
  | --- Individually declared dependencies
    packageDependencies {dependencies : List Dependency};

--- A ;Package; defines the configuration for a Juvix package
type Package :=
  mkPackage {
    --- The name of the package
    name : String;
    --- The version for the package
    version : SemVer;
    --- The dependencies of this package
    dependencies : PackageDependencies;
    --- A path to the Main module for this package
    main : Maybe String;
    --- A path to a directory where Juvix should output intermediate build products
    buildDir : Maybe String
  };

--- Construct a ;Package; with useful default arguments.
defaultPackage
  {name : String := "my-project"}
  {version : SemVer := defaultVersion}
  {dependencies : PackageDependencies := packageDependencies [defaultStdlib]}
  {main : Maybe String := nothing}
  {buildDir : Maybe String := nothing}
  : Package := mkPackage name version dependencies main buildDir;

--- Construct a ;Package; with dependencies from a ;PackageSet;.
usingPackageSet
  (packageSet : PackageSet)
  (dependencies : List String)
  {extraDependencies : List Dependency := []}
  {overridingDependencies : List OverridingDependency := []}
  {name : String := "my-project"}
  {version : SemVer := defaultVersion}
  {main : Maybe String := nothing}
  {buildDir : Maybe String := nothing}
  : Package :=
  let
    packageDependencies :=
      packageSetDependencies@{
        packageSet;
        dependencies;
        extraDependencies;
        overridingDependencies
      };
  in mkPackage@{
    name;
    version;
    dependencies := packageDependencies;
    main;
    buildDir
  };

--- Construct a ;Package; using the default registry for dependencies.
usingDefaultRegistry
  (packageSetVersion : SemVer)
  (dependencies : List String)
  {extraDependencies : List Dependency := []}
  {overridingDependencies : List OverridingDependency := []}
  {name : String := "my-project"}
  {version : SemVer := defaultVersion}
  {main : Maybe String := nothing}
  {buildDir : Maybe String := nothing}
  : Package :=
  let
    packageSet :=
      mkPackageSet@{
        registryLocation := defaultLocation;
        packageSetVersion
      };
  in usingPackageSet@{
    packageSet;
    dependencies;
    extraDependencies;
    overridingDependencies;
    name;
    version;
    main;
    buildDir
  };
