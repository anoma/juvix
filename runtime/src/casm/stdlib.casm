-- Juvix runtime for Cairo assembly

-- Closure layout: [ fuid | 9 - sargs | 9 - argsnum | arguments... ]
--  fuid -- function id which also indicates the relative jump
--          offset for closure call (see juvix_call_closure below)
--  sargs -- number of arguments stored in the closure
--  argsnum -- number of arguments left
-- sargs + argsnum = total numer of arguments for the function
-- Maximum number of function arguments: 8

-- after calling juvix_get_regs:
-- [ap - 4] = fp
-- [ap - 3] = pc
-- [ap - 2] = ap - 2
juvix_get_regs:
  call juvix_get_ap_reg
  ret
juvix_get_ap_reg:
  ret

-- [fp - 3]: closure
-- [fp - 4]: n = the number of arguments to extend with
-- [fp - 4 - k]: argument n - k - 1 (reverse order!)
juvix_extend_closure:
  -- copy stored args reversing them;
  -- to copy the stored args to the new closure
  -- we need to jump forward, so the stored args
  -- need to be available at consecutive memory
  -- addresses backwards
  jmp rel [[fp - 3] + 1]
  [ap] = [[fp - 3] + 10]; ap++
  [ap] = [[fp - 3] + 9]; ap++
  [ap] = [[fp - 3] + 8]; ap++
  [ap] = [[fp - 3] + 7]; ap++
  [ap] = [[fp - 3] + 6]; ap++
  [ap] = [[fp - 3] + 5]; ap++
  [ap] = [[fp - 3] + 4]; ap++
  [ap] = [[fp - 3] + 3]; ap++
  -- the following ensures continuous memory use
  -- with a compile-time contant offset for local
  -- variables
  [ap] = 10; ap++
  [ap] = [[fp - 3] + 1]; ap++
  [ap] = [ap - 2] - [ap - 1]; ap++
  jmp rel [ap - 1]
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  [ap] = [ap - 1]; ap++
  -- now ap = fp + 11
  -- alloc
  call juvix_get_regs
  -- now ap = fp + 15
  -- [fp + 15] = pointer to new closure
  [ap] = [ap - 2] + 8; ap++
  -- [fp + 16] = 9 - sargs
  [ap] = [[fp - 3] + 1]; ap++
  -- [fp + 17] = 9 - argsnum (expected)
  [ap] = [[fp - 3] + 2]; ap++
  -- [fp + 18] = 9
  [ap] = 9; ap++
  -- [fp + 19] = sargs
  [ap] = [fp + 18] - [fp + 16]; ap++
  -- [fp + 20] = 9 - n
  [ap] = [fp + 18] - [fp - 4]; ap++
  -- closure header
  [ap] = [[fp - 3]]; ap++
  [ap] = [fp + 16] - [fp - 4]; ap++
  [ap] = [fp + 17] + [fp - 4]; ap++
  -- copy stored args: jmp rel (9 - sargs)
  jmp rel [fp + 16]
  [ap] = [fp + 7]; ap++
  [ap] = [fp + 6]; ap++
  [ap] = [fp + 5]; ap++
  [ap] = [fp + 4]; ap++
  [ap] = [fp + 3]; ap++
  [ap] = [fp + 2]; ap++
  [ap] = [fp + 1]; ap++
  [ap] = [fp]; ap++
  -- copy extra args: jmp rel (9 - extra args num)
  jmp rel [fp + 20]
  [ap] = [fp - 12]; ap++
  [ap] = [fp - 11]; ap++
  [ap] = [fp - 10]; ap++
  [ap] = [fp - 9]; ap++
  [ap] = [fp - 8]; ap++
  [ap] = [fp - 7]; ap++
  [ap] = [fp - 6]; ap++
  [ap] = [fp - 5]; ap++
  -- return value
  [ap] = [fp + 15]; ap++
  ret

-- [fp - 3]: closure;
-- [fp - 3 - k]: argument k to closure call
-- assumption: 3 builtins passed at the end of args
juvix_call_closure:
  -- jmp rel (9 - argsnum)
  jmp rel [[fp - 3] + 2]
  -- builtins
  [ap] = [fp - 14]; ap++
  [ap] = [fp - 13]; ap++
  [ap] = [fp - 12]; ap++
  -- args
  [ap] = [fp - 11]; ap++
  [ap] = [fp - 10]; ap++
  [ap] = [fp - 9]; ap++
  [ap] = [fp - 8]; ap++
  [ap] = [fp - 7]; ap++
  [ap] = [fp - 6]; ap++
  [ap] = [fp - 5]; ap++
  [ap] = [fp - 4]; ap++
  -- jmp rel (9 - sargs)
  jmp rel [[fp - 3] + 1]
  [ap] = [[fp - 3] + 10]; ap++
  [ap] = [[fp - 3] + 9]; ap++
  [ap] = [[fp - 3] + 8]; ap++
  [ap] = [[fp - 3] + 7]; ap++
  [ap] = [[fp - 3] + 6]; ap++
  [ap] = [[fp - 3] + 5]; ap++
  [ap] = [[fp - 3] + 4]; ap++
  [ap] = [[fp - 3] + 3]; ap++
  jmp rel [[fp - 3]]
-- The above jump needs to be the last instruction in this file.
-- Calls to all possible functions are appended here by the
-- JuvixReg to CASM translation.
