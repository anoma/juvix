module Collatz;

import Stdlib.Prelude open;
import Stdlib.Data.Nat.Ord open;

collatzNext : Nat → Nat
  | n := if (mod n 2 == 0) (div n 2) (3 * n + 1);

collatz : Nat → Nat
  | zero := zero
  | (suc zero) := suc zero
  | n := collatzNext n;

terminating
run : (Nat → Nat) → Nat → IO
  | _ (suc zero) :=
    printNatLn 1 >> printStringLn "Finished!"
  | f n := printNatLn n >> run f (f n);

welcome : String :=
  "Collatz calculator\n------------------\n\nType a number then ENTER";

resultHeading : String := "Collatz sequence:";

main : IO :=
  printStringLn welcome
    >> readLn
      λ {s :=
        printStringLn resultHeading
          >> run collatz (stringToNat s)};
